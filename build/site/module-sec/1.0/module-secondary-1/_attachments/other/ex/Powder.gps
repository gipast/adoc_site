
;      POWDER.GPS, by Gerard F. Cummings
100 *************************************
110 *                                   *
120 *   BRAND LOYALTY BY CUSTOMERS      *
130 *************************************
150         RMULT        98851
160 CHECK   TABLE        X$BRAND,1,1,8
170 *
175 TRANSITIONS MATRIX   ,7,7                ;STATE TRANSITION TABLE
176 *
180 POWDER  FUNCTION     X$BRAND,M7           ;PICK BRAND TRANSITION
1,FN$SUDS/2,FN$BRANDX/3,FN$BUBBLES/4,FN$CLEARSHINE
5,FN$CLEANPLUS/6,FN$MARVEL/7,FN$SOAPY
188 *
190 ******************************************
195 *
200 SUDS     FUNCTION     RN1,D7              ;TRANSITION FROM SUDS
0.390,1/.510,2/.680,3/.810,4/.910,5/.950,6/1.0,7
210 *
220 BRANDX     FUNCTION     RN1,D7            ;TRANSITION FROM BRANDX
0.130,1/.450,2/.550,3/.700,4/.820,5/.910,6/1.0,7
230 *
240 BUBBLES  FUNCTION     RN1,D7              ;TRANSITION FROM BUBBLES
0.150,1/.290,2/.540,3/.680,4/.850,5/.930,6/1.0,7
250 *
260 CLEARSHINE    FUNCTION     RN1,D7         ;TRANSITION FROM CLEARSHINE
0.110,1/.210,2/.300,3/.700,4/.780,5/.870,6/1.0,7
270 *
280 CLEANPLUS     FUNCTION     RN1,D7         ;TRANSITION FROM CLEANPLUS
0.050,1/.170,2/.330,3/.420,4/.790,5/.930,6/1.0,7
290 *
300 MARVEL   FUNCTION     RN1,D7              ;TRANSITION FROM MARVEL
0.160,1/.290,2/.370,3/.420,4/.580,5/.860,6/1.0,7
310 *
320 SOAPY    FUNCTION     RN1,D7              ;TRANSITION FROM SOAPY
0.080,1/.180,2/.270,3/.370,4/.440,5/.570,6/1.0,7
330 *
340 RECORD  FUNCTION     X$BRAND,L7            ;RECORDS NUMBERS
1,SUDS/2,BRANDX/3,BUBBLES/4,CLEARSHINE/5,CLEANPLUS/6,MARVEL/7,SOAPY
350 ******************************************
360         INITIAL      X$BRAND,1
370         GENERATE     1,,,,2              ;ONE XAC EVERY MINUTE
380         TABULATE     CHECK
385         SAVEVALUE    OLDBRAND,X$BRAND    ;SAVE OLD BRAND
390         SAVEVALUE    BRAND,FN$POWDER     ;FIND NEW BRAND
400         SAVEVALUE    FN$RECORD+,1        ;SAVEVALUE OF CHOSEN BRAND UP BY 1
405         MSAVEVALUE   TRANSITIONS+,X$OLDBRAND,X$BRAND,1 ;UPDATE TRANSITIONS
410         TERMINATE    1
450 ******************************************
460         REPORT       POWDER.REP
